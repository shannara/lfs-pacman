# $Id$
# Maintainer: Stanislas Leduc <stanislas.leduc@balinor.net>

pkgname=gettext
pkgver=0.19.8.1
pkgrel=1
pkgdesc="GNU internationalization library"
arch=('x86_64')
url="http://www.gnu.org/software/gettext/"
groups=('base' 'base-devel')
depends=('gcc-libs' 'acl' 'bash')
options=(!docs)
license=('GPL')
source=(${pkgname}-${pkgver}.tar.xz)
md5sums=('df3f5690eaa30fd228537b00cb7b7590')

prepare() {
    cd "${pkgname}-${pkgver}"
    sed -i '/^TESTS =/d' gettext-runtime/tests/Makefile.in
    sed -i 's/test-lock..EXEEXT.//' gettext-tools/gnulib-tests/Makefile.in
}

build() {
    cd "${pkgname}-${pkgver}"
    ./configure --prefix=/usr    \
		--disable-static \
		--docdir=/usr/share/doc/gettext-${pkgver}
    make
}

check() {
    cd "${pkgname}-${pkgver}"
    make check 2>&1 | tee ${pkgname}-${pkgver}-check-log
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR=${pkgdir} install
    chmod -v 0755 ${pkgdir}/usr/lib/preloadable_libintl.so
}
