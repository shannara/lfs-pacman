# $Id$
# Maintainer: Stanislas Leduc <stanislas.leduc@balinor.net>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc

pkgname=glibc
pkgver=2.27
pkgrel=1
pkgdesc="GNU C Library"
arch=('x86_64')
url="http://www.gnu.org/software/libc"
groups=('base')
options=(!strip staticlibs)
depends=('linux-api-headers>=4.14.15' tzdata filesystem)
license=('GPL', 'LGPL')
backup=(etc/ld.so.conf)
source=(${pkgname}-${pkgver}.tar.xz
	glibc-2.27-fhs-1.patch
	nsswitch.conf
	ld.so.conf)
md5sums=('898cd5656519ffbc3a03fe811dd89e82'
         '9a5997c3452909b1769918c759eff8a2'
         'fb3976f942e00d20efb9b6f00f893a29'
         'ccd13d3e7f6fda164efdf6fb3159eaa6')
install=glibc.install

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i ../glibc-2.27-fhs-1.patch
}

build() {
  cd "${pkgname}-${pkgver}"

  mkdir -v build
  cd build

  GCC_INCDIR=/usr/lib/gcc/x86_64-pc-linux-gnu/7.3.0/include
  CC="gcc" \
  ../configure --prefix=/usr        \
    --with-headers=/usr/include     \
    --disable-werror                \
    --enable-kernel=4.14.13         \
    --enable-stack-protector=strong \
      libc_cv_slibdir=/lib
  unset GCC_INCDIR
  make
}

check() {
  cd "${pkgname}-${pkgver}"/build
  make check 2>&1 | tee ${pkgname}-${pkgver}-check-log
}

package() {
  cd "${pkgname}-${pkgver}"/build

  install -v -dm755 ${pkgdir}/etc
  touch ${pkgdir}/etc/ld.so.conf
  make install_root=${pkgdir} install

  install -v -dm755 ${pkgdir}/etc/nsdc
  install -v -m644 ${srcdir}/${pkgname}-${pkgver}/nscd/nscd.conf ${pkgdir}/etc/nscd.conf
  install -v -dm755 ${pkgdir}/var/cache/nscd

  install -v -dm755 ${pkgdir}/usr/lib/locale

  install -v -m644 ${srcdir}/nsswitch.conf ${pkgdir}/etc/nsswitch.conf

  install -v -m644 ${srcdir}/ld.so.conf ${pkgdir}/etc/
  install -v -dm755 ${pkgdir}/etc/ld.so.conf.d

  # Do not strip the following files for improved debugging support
  # ("improved" as in not breaking gdb and valgrind...):
  #   ld-${pkgver}.so
  #   libc-${pkgver}.so
  #   libpthread-${pkgver}.so
  #   libthread_db-1.0.so

  if check_option 'debug' n; then
    cd "$pkgdir"
    strip $STRIP_BINARIES usr/bin/{gencat,getconf,getent,iconv} \
                          usr/sbin/{iconvconfig,nscd} \
                          sbin/{ldconfig,sln} \
                          usr/bin/{locale,localedef,makedb} \
                          usr/bin/{pcprofiledump,pldd,sprof} \
                          usr/libexec/getconf/*

    strip $STRIP_STATIC usr/lib/lib{anl,BrokenLocale,c{,_nonshared},crypt}.a \
                        usr/lib/lib{dl,g,mcheck,pthread{,_nonshared}}.a \
                        usr/lib/lib{resolv,rt,util}.a \
                        usr/lib/lib{m-${pkgver},mvec{,_nonshared}}.a

    strip $STRIP_SHARED usr/lib/lib{anl,BrokenLocale,cidn,crypt}.so \
                        lib/lib{anl,BrokenLocale,cidn,crypt}-${pkgver}.so \
                        usr/lib/libnss_{compat,db,dns,files,hesiod,nis,nisplus}.so \
                        lib/libnss_{compat,db,dns,files,hesiod,nis,nisplus}-${pkgver}.so \
                        usr/lib/lib{dl,m,nsl,resolv,rt,util}.so \
                        lib/lib{dl,m,nsl,resolv,rt,util}-${pkgver}.so \
                        usr/lib/lib{memusage,pcprofile,SegFault}.so \
                        lib/libmvec-${pkgver}.so \
                        usr/lib/{audit,gconv}/*.so usr/lib/libmvec.so || true
  fi
}
