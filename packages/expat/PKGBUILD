# $Id$
# Maintainer: Stanislas Leduc <stanislas.leduc@balinor.net>

pkgname=expat
pkgver=2.2.5
pkgrel=1
pkgdesc="An XML parser library"
arch=('x86_64')
url="http://expat.sourceforge.net/"
license=('custom')
depends=(glibc)
source=(${pkgname}-${pkgver}.tar.bz2)
md5sums=('789e297f547980fc9ecc036f9a070d49')

prepare() {
    cd "${pkgname}-${pkgver}"
    sed -i 's|usr/bin/env |bin/|' run.sh.in
}

build() {
    cd "${pkgname}-${pkgver}"
    ./configure --prefix=/usr --disable-static
    make
}

check() {
    cd "${pkgname}-${pkgver}"
    make check 2>&1 | tee ${pkgname}-${pkgver}-check-log
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR=${pkgdir} install

    install -vdm755 ${pkgdir}/usr/share/doc/expat-${pkgver}
    install -vm644 doc/*.{html,png,css} ${pkgdir}/usr/share/doc/expat-${pkgver}
}
