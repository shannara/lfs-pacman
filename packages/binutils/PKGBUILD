# $Id$
# Maintainer: Stanislas Leduc <stanislas.leduc@balinor.net>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc

pkgname=binutils
pkgver=2.30
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=('x86_64')
url="http://www.gnu.org/software/binutils/"
groups=('base-devel')
depends=('glibc>=2.27' zlib)
license=('GPL')
options=(staticlibs)
source=(${pkgname}-${pkgver}.tar.xz)
md5sums=('ffc476dd46c96f932875d1b2e27e929f')

build() {
    cd "${pkgname}-${pkgver}"

    mkdir -v build
    cd build

    ../configure --prefix=/usr       \
		 --enable-gold       \
		 --enable-ld=default \
		 --enable-plugins    \
		 --enable-shared     \
		 --disable-werror    \
		 --with-system-zlib

    make tooldir=/usr
}

check() {
   cd "${pkgname}-${pkgver}/build"
   make -k check 2>&1 | tee ${pkgname}-${pkgver}-check-log
}

package() {
    cd "${pkgname}-${pkgver}/build"
    make prefix=${pkgdir}/usr tooldir=${pkgdir}/usr install
}
